/*
    c) CMD: modes Task1ab.modest -S ASAP -R Uniform
    
    n       := number of questions asked in a row
    ign     := number of ignores in a row
    total   := total number of questions asked in a run
    
    c)  ExTimeToLeave: 213.17s
        ExTimeToLeaveOr5Q: 150.17s
*/

int ign = 0;
int n = 0;
int total = 0;
bool leave_hall;

action raise_hand, ack_q, ign_q;
action ask_q, stop_asking, answer_q;

// Task c)
property ExTimeToLeave = Xmax(time | leave_hall);
property ExTimeToLeaveOr5Q = Xmax(time | (leave_hall || total >= 5));

process Student() {
    alt {
    :: when(ign < 3) raise_hand;
        alt {
        :: ack_q {= n = 0, ign = 0 =};
            do {
                palt {
                :1: ask_q; delay(Uniform(30, 120)) answer_q {= n = n + 1, total = total + 1 =}
                :n: stop_asking; break
                }
            }
        :: ign_q {= ign = ign+1 =} 
        }; delay(Exponential(1/300)) Student() // the student should leave immediately???
    :: when(ign >= 3) {= leave_hall = true =}
    }
}

process Lecturer() {
    raise_hand;
    palt {
    :1: ack_q; 
        do {
            :: ask_q; answer_q
            :: stop_asking; break
        }
    :3: ign_q
    }; Lecturer()
}

par {
:: Student()
:: Lecturer()
}
